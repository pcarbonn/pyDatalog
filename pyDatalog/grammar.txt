
/* http://railroad.my28msec.com/rr/ui  */

datalog_statement ::= ('+' | '-') fact 
  | head '<=' body
  | function '=' expression

fact ::= literal_without_variable

head ::= literal | '(' function '==' ('len' | 'sum' | 'concat' | 'min' | 'max') '(' arguments ')' ')'
body ::= ( '~' ? ) ( literal 
           | '(' variable ('<' | '<=' | '==' | '~=' | '>='  | '>') expression ')'
           | '(' variable 'in' range ')'
         ) 
         ( '&' ( '~' ? ) ( literal 
                 | '(' variable ('<' | '<=' | '==' | '~=' | '>='  | '>') expression ')' 
                 | '(' variable 'in' range ')' )
         )*

literal ::= predicate '(' (variable | constant) (',' (variable | constant))* ')'
  | '(' function '==' (variable | constant) ')'
  
predicate ::= simple_predicate | simple_predicate '.' simple_predicate

function ::= predicate '[' (variable | constant) (',' (variable | constant))*  ']'

expression ::= (constant 
  | variable 
  | function
  | '(' expression ')'  
  | '(' lambda-expression ')' 
  | expression ([+*/-] | '//') expression
  )
  
simple_predicate ::= [a-fA-F_] [0-9a-fA-F_]*
constant ::= [a-f] [0-9a-fA-F_]* | ["'] char ["']
variable::= [A-F_] [0-9a-fA-F_]*
